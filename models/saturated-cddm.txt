model{
# Prior distribution: boundary
  for(ss in 1:n_speed){
    mu_eta[ss]    ~ dnorm(0, 1)
    sigma_eta[ss] ~ dunif(0, 3)
    tau_eta[ss]   = 1/sigma_eta[ss]^2
    
    for(ii in 1:n_par){
      eta_tmp[ii, ss] ~ dnorm(mu_eta[ss], tau_eta[ss])
      eta[ii, ss]     = exp(eta_tmp[ii, ss])
    }
  }

# Prior distribution: drift
  for(dd in 1:n_difficulty){
    mu_delta[dd]    ~ dnorm(0, 1)
    sigma_delta[dd] ~ dunif(0,3) 
    tau_delta[dd]   = 1/sigma_delta[dd]^2
    
    for(ii in 1:n_par){
      delta_tmp[ii, dd] ~ dnorm(mu_delta[dd], tau_delta[dd])
      delta[ii,dd]      = exp(delta_tmp[ii, dd])
    }
  }
  
# Prior distribution: non-decision time
  for(ii in 1:n_par){
    t0[ii] ~ dexp(1)
  }
  
# Prior distribution: mixture of response angle
  for(cc in 1:n_cues){
    for(ii in 1:n_par){
      omega[ii, cc] ~ dbeta(1,1)
    }
  }
  
# Prior distribution: variance percived angle

  for(cc in 1:n_cues){
    for(ii in 1:n_par){
      var_pos[ii, cc] ~ dunif(0, var_inf)
    }
  }
  
# Prior distribution: variance of percived cue

  for(aa in 1:n_abs_cues){
    for(ii in 1:n_par){
      var_cue[ii,aa] ~ dunif(0, var_inf)
    }
  }

  for(t in 1:n){
# Prior distribution: angles and mizture component
    z[t]            ~ dbern(omega[i[t], c[t]])
    theta_tmp2[t,1] ~ dnorm(position[t], 1/var_pos[i[t], c[t]])
    theta_tmp2[t,2] ~ dnorm(cue_position[t], 1/var_cue[i[t], ac[t]])
    
    theta_tmp1[t,1] = ifelse(theta_tmp2[t,1]<0, theta_tmp2[t,1]+6.283185, theta_tmp2[t,1])
    theta_tmp1[t,2] = ifelse(theta_tmp2[t,2]<0, theta_tmp2[t,2]+6.283185, theta_tmp2[t,2])
    
    theta[t,1] = ifelse(theta_tmp1[t,1]>6.283185, theta_tmp1[t,1]-6.283185, theta_tmp1[t,1])
    theta[t,2] = ifelse(theta_tmp1[t,2]>6.283185, theta_tmp1[t,2]-6.283185, theta_tmp1[t,2])
    
# Likelihood function
    y[t,1:2] ~ dcddm(delta[i[t], d[t]],
                     eta[i[t], s[t]], 
                     t0[i[t]],
                     theta[t, (z[t] + 1)])
  }
}
